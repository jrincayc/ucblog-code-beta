;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                        ;;
;;             BERKELEY LOGO              ;;
;;            Macro Unit Tests            ;;
;;                                        ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


InstallSuite [Macros] [Tests.Macro.Setup]



;; The list of all Macro unit tests
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

MAKE "Tests.Macro [
  ;list tests here
  Tests.Macro.OutputStopWorksAsExpected
  Tests.Macro.PlainStopErrorsAsExpected
  Tests.Macro.FunctionStopErrorsAsExpected
  Tests.Macro.PlainStopInnerWorksAsExpected
  Tests.Macro.RepeatStopWorksAsExpected
  Tests.Macro.RepeatEarlyStopWorks
  Tests.Macro.IfStopWorksAsExpected
]

;; Test Suite setup procedure, main entry
;; point for all tests in this suite
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to Tests.Macro.Setup
  RunTests :Tests.Macro
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                 ;;
;; HELPERS, MISC                   ;;
;;                                 ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

.MACRO Tests.Macro._OutputStopMacro
  OUTPUT [ STOP ]
END

TO Tests.Macro._CallOutputStop
  Tests.Macro._OutputStopMacro
  OUTPUT 1
END

.MACRO Tests.Macro._PlainStopMacro
  STOP
END

TO Tests.Macro._StopFunction
  STOP
END

TO Tests.Macro._CallStopFunction
  OUTPUT Tests.Macro._StopFunction
END


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                                 ;;
;; ADD INDIVIDUAL UNIT TESTS BELOW ;;
;;                                 ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;all tests must return T/F indicating success/failure

TO Tests.Macro.OutputStopWorksAsExpected
  LOCALMAKE "result RUNRESULT [ Tests.Macro._CallOutputStop ]

  OUTPUT EMPTY? :result
END

TO Tests.Macro.PlainStopErrorsAsExpected
  CATCH "Error [ Tests.Macro._PlainStopMacro ]
  LOCALMAKE "err ERROR

  ; Message 29 is "Macro returned %s instead of a list"
  OUTPUT (AND [NOT EMPTY? :err]
              [EQUAL? FIRST :err 29])
END


;; Not strictly a macro test; but, the functionality to handle this error
;; is closely related to the functionality to catch a macro directly
;; calling stop

TO Tests.Macro.FunctionStopErrorsAsExpected
  CATCH "Error [ Tests.Macro._CallStopFunction ]
  LOCALMAKE "err ERROR

  ; Message 5 is "%p didn't output to %p"
  OUTPUT (AND [NOT EMPTY? :err]
              [EQUAL? FIRST :err 5])
END

;; Test plain returning

TO Tests.Macro._inner1
 make "ret "false
 stop
end

To Tests.Macro._middle1
 Tests.Macro._inner1
 make "ret "true
end

TO Tests.Macro.PlainStopInnerWorksAsExpected
 make "ret "false
 Tests.Macro._middle1
 output :ret
end

;; Test returning from a repeat

TO Tests.Macro._inner2
 make "ret "false
 repeat 1 [stop]
end

TO Tests.Macro._middle2
 Tests.Macro._inner2
 make "ret "true
end

TO Tests.Macro.RepeatStopWorksAsExpected
 make "ret "false
 Tests.Macro._middle2
 output :ret
end

;; Test early return from repeat

to Tests.Macro._inner3
 make "ret "false
 repeat 3 [stop]
end

to Tests.Macro._middle3
 Tests.Macro._inner3
 make "ret "true
end

to Tests.Macro.RepeatEarlyStopWorks
 make "ret "false
 Tests.Macro._middle3
 output :ret
end

to Tests.Macro._inner4
 make "ret "false
 if "true [stop]
end

to Tests.Macro._middle4
 Tests.Macro._inner4
 make "ret "true
end

to Tests.Macro.IfStopWorksAsExpected
 make "ret "false
 Tests.Macro._middle4
 output :ret
end
